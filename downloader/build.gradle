apply plugin: 'com.android.library'

def getSdkDir() {
    def path
    def localProperties = new File("local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream { instr ->
            properties.load(instr)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        if (sdkDir) {
            path = sdkDir
        } else {
            path = "$System.env.ANDROID_HOME"
        }
    } else {
        path = "$System.env.ANDROID_HOME"
    }
    return path
}

def getLibPath() {
    def path = getSdkDir()
    def ret = path + "/extras/google/market_apk_expansion/downloader_library"
    return ret
}
def libPath = getLibPath()

def getZipPath() {
    def path = getSdkDir()
    def ret = path + "/extras/google/market_apk_expansion/zip_file"
    return ret
}
def zipPath = getZipPath()

android {
    compileSdkVersion 22
    buildToolsVersion "25.0.2"
    //useLibrary "org.apache.http.legacy"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 22
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            manifest.srcFile "$libPath/AndroidManifest.xml"
            java {
                srcDir "${libPath}/src"
            }
            java {
                srcDir "${zipPath}/src"
            }
            res.srcDirs = [
                "${libPath}/res",
                "${zipPath}/res"
            ]
            aidl.srcDirs = [
                "${libPath}/aidl"
            ]
            assets.srcDirs = [
                "${libPath}/assets"
            ]
            jniLibs.srcDirs = []
            resources.srcDirs = []
            renderscript.srcDirs = []
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile project(':licensing')
    compile 'com.android.support:appcompat-v7:22.+'
    testCompile 'junit:junit:4.12'
}
