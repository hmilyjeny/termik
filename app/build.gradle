apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "org.tensorflow.demo"
        //applicationId "uk.co.mishurov.catdetector"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                abiFilters "x86_64"
                //abiFilters "x86_64", "x86", "armeabi-v7a", "arm64-v8a"//, "armeabi", "mips", "mips64"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            // TensorFlow Java API sources.
            java {
                srcDir '../distribution/tensorflow/tensorflow/tensorflow/java/src/main/java'
                exclude '**/examples/**'
            }
            // Android TensorFlow wrappers, etc.
            java {
                srcDir '../distribution/tensorflow/tensorflow/tensorflow/contrib/android/java'
            }

            java {
                srcDir 'src/main/java'
            }
            manifest.srcFile 'src/main/AndroidManifest.xml'
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']
            jniLibs.srcDirs = [
                '../distribution/tensorflow/out/native/libtensorflow_inference.so',
                '../distribution/tensorflow/out/native/libtensorflow_demo.so'
            ]
        }
    }
}

task unzipAssets(type: Copy) {
    from(zipTree('../distribution/tensorflow/out/tensorflow_demo.apk')) {
        include 'assets/tensorflow_inception_graph.pb'
        include 'assets/imagenet_comp_graph_label_strings.txt'
    }
    into 'src/main/'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
}
